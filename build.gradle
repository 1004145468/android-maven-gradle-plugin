
plugins {
	id "nu.studer.plugindev" version "1.0.4"
	id "com.gradle.plugin-publish" version "0.9.1"
	id "org.ysb33r.gradletest" version "0.5.4"
}

apply plugin: "nu.studer.plugindev"
apply plugin: "jacoco"

repositories {
	mavenCentral()
	mavenLocal()
}

sourceSets {
	integTest {
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	integTestCompile.extendsFrom testCompile
	integTestRuntime.extendsFrom testRuntime
}

dependencies {
	
	// from gradle/gradle/dependencies.gradle
	compile dependencies.module("org.apache.maven:maven-core:3.0.4") {
		dependency "org.apache.maven:maven-settings:3.0.4@jar"
		dependency "org.apache.maven:maven-settings-builder:3.0.4@jar"

		//plexus:
		dependency "org.codehaus.plexus:plexus-utils:2.0.6@jar"
		dependency "org.codehaus.plexus:plexus-interpolation:1.14@jar"
		dependency "org.codehaus.plexus:plexus-component-annotations:1.5.5@jar"
		dependency "org.codehaus.plexus:plexus-container-default:1.5.5@jar"
		dependency "org.codehaus.plexus:plexus-classworlds:2.4@jar"

		//sonatype plexus
		dependency "org.sonatype.plexus:plexus-cipher:1.7@jar"
		dependency "org.sonatype.plexus:plexus-sec-dispatcher:1.3@jar"

		//core:
		dependency "org.apache.maven:maven-core:3.0.4@jar"
		dependency "org.apache.maven:maven-compat:3.0.4@jar"
		dependency "org.apache.maven:maven-model-builder:3.0.4@jar"
		dependency "org.apache.maven:maven-model:3.0.4@jar"

		//somewhat core:
		dependency "org.apache.maven:maven-artifact:3.0.4@jar"
		dependency "org.apache.maven:maven-repository-metadata:3.0.4@jar"
		dependency "org.apache.maven:maven-plugin-api:3.0.4@jar"
		dependency "org.apache.maven:maven-aether-provider:3.0.4@jar"

		dependency 'org.apache.maven.wagon:wagon-file:2.4@jar'
		dependency 'org.apache.maven.wagon:wagon-http:2.4@jar'
		dependency 'org.apache.maven.wagon:wagon-provider-api:2.4@jar'
		dependency 'org.apache.maven.wagon:wagon-http-shared4:2.4@jar'

		//eather:
		dependency "org.sonatype.aether:aether-api:1.13.1@jar"
		dependency "org.sonatype.aether:aether-impl:1.13.1@jar"
		dependency "org.sonatype.aether:aether-spi:1.13.1@jar"
		dependency "org.sonatype.aether:aether-util:1.13.1@jar"
		dependency 'org.sonatype.aether:aether-connector-wagon:1.13.1@jar'
	}
	
	testCompile	'junit:junit:4.12',
				'org.hamcrest:hamcrest-all:1.3'
	gradleTest 'com.android.tools.build:gradle:1.5.0'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

plugindev {
	pluginId = 'com.github.dcendents.android-maven'
	pluginImplementationClass 'org.gradle.api.plugins.AndroidMavenPlugin'
	pluginName 'Gradle Android Maven Plugin'
	pluginDescription 'Modification to the standard Maven plugin to be compatible with android-library projects (aar).'
	pluginLicenses 'Apache-2.0'
	pluginTags 'gradle', 'plugin', 'android', 'maven'
	authorId 'dcendents'
	authorName 'Daniel Beland'
	authorEmail 'dcendents@gmail.com'
	projectUrl 'https://github.com/dcendents/android-maven-gradle-plugin'
	projectInceptionYear '2013'
	done() // do not omit this
}

ext {
	// Set values in ~/.gradle/gradle.properties, set default values otherwise for build to work
	bintrayUsername = project.properties['bintrayUsername'] != null ? project.properties['bintrayUsername'] : ""
	bintrayApiKey = project.properties['bintrayApiKey'] != null ? project.properties['bintrayApiKey'] : ""
	bintrayPassphrase = project.properties['bintrayPassphrase'] != null ? project.properties['bintrayPassphrase'] : ""
}

bintray {
	user = "${bintrayUsername}"
	key = "${bintrayApiKey}"

	dryRun = false
	publish = true
	pkg {
		repo = 'gradle-plugins'
		name = 'com.github.dcendents:android-maven-plugin'
		version {
			gpg {
				sign = true
				passphrase = "${bintrayPassphrase}"
			}
		}
	}
}

pluginBundle {
	website = 'https://github.com/dcendents/android-maven-gradle-plugin'
	vcsUrl = 'https://github.com/dcendents/android-maven-gradle-plugin'

	description = 'Modification to the standard Maven plugin to be compatible with android-library projects (aar).'

	plugins {
		androidMavenPlugin {
			id = 'com.github.dcendents.android-maven'
			displayName = 'Gradle Android Maven plugin'
			tags = ['maven', 'android']
			version = version
		}
	}

	mavenCoordinates {
		groupId = group
		artifactId = theName
		version = version
	}
}

ext {
	gradleVersions = '2.4,2.5,2.6,2.7,2.8,2.9,2.10,2.11,2.12,2.13'
}

task integTest(type: Test) {
	systemProperty 'gradle.versions', gradleVersions
	testClassesDir = sourceSets.integTest.output.classesDir
	classpath = sourceSets.integTest.runtimeClasspath
	reports {
		junitXml.destination = "$buildDir/integration-test-results"
		reports.html.destination = "$buildDir/reports/integrationTests"
	}
}

gradleTest {
	versions gradleVersions.split(',')
}

gradleLocations {
	searchGradleUserHome = true
	includeGradleHome = false
	searchGvm = false
	download = true
	downloadToGradleUserHome = true
	useGradleSite = true
}

gradleTest.dependsOn jar
integTest.dependsOn gradleTest
check.dependsOn integTest

jacoco {
	toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled true
		html.destination "${buildDir}/jacocoHtml"
	}
}

jar.dependsOn test
jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
