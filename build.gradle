
buildscript {
	repositories {
		maven {
			url "https://bintray.com/artifact/download/dcendents/gradle-plugins"
		}
		jcenter()
	}
	dependencies {
		classpath "com.github.dcendents:gradle-plugindev-plugin:1.0.4.20150505"
	}
}

apply plugin: "nu.studer.plugindev"

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	testCompile	'junit:junit:4.11'
}
sourceCompatibility = 1.6
targetCompatibility = 1.6

plugindev {
	pluginId = 'com.github.dcendents.android-maven'
	pluginImplementationClass 'org.gradle.api.plugins.AndroidMavenPlugin'
	pluginName 'Gradle Android Maven Plugin'
	pluginDescription 'Modification to the standard Maven plugin to be compatible with android-library projects (aar).'
	pluginLicenses 'Apache-2.0'
	pluginTags 'gradle', 'plugin', 'android', 'maven'
	authorId 'dcendents'
	authorName 'Daniel Beland'
	authorEmail 'dcendents@gmail.com'
	projectUrl 'https://github.com/dcendents/android-maven-plugin'
	projectInceptionYear '2013'
	done() // do not omit this
}

ext {
	// Set values in ~/.gradle/gradle.properties, set default values otherwise for build to work
	bintrayUsername = project.properties['bintrayUsername'] != null ? project.properties['bintrayUsername'] : ""
	bintrayApiKey = project.properties['bintrayApiKey'] != null ? project.properties['bintrayApiKey'] : ""
	bintrayPassphrase = project.properties['bintrayPassphrase'] != null ? project.properties['bintrayPassphrase'] : ""
}

bintray {
	user = "${bintrayUsername}"
	key = "${bintrayApiKey}"

	dryRun = false
	publish = true
	pkg {
		repo = 'gradle-plugins'
		name = 'com.github.dcendents:android-maven-plugin'
		version {
			gpg {
				sign = true
				passphrase = "${bintrayPassphrase}"
			}
		}
	}
}

test.dependsOn jar

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
